config {
    type: "table",
    description: "Combines request metrics",
    columns: {
        assessed_date: "date of award or decline",
        assessment_id: "assessment_id",
        country_name: "country applicant qualified in",
        created_at: "request created at",
        days_waiting_to_receive: "days waiting for information to be received",
        days_waiting_to_request: "days waiting for information to be requested",
        days_waiting_to_review: "days waiting for information to be reviewed",
        days_waiting_to_verify: "days waiting for information to be verified",
        reference: "application reference number",
        request_type: "type of request",
        started_at_date: "started at date",
        submitted_at_date: "submitted at date",
        teaching_authority_provides_written_statement: "teaching authority provides written statement",
    },

}

WITH
  fi_days_in_status AS (
  SELECT
    assessment_id,
    "further_information" AS request_type,
    created_at,
    NULL AS days_waiting_to_request,
    DATE_DIFF(received_at, requested_at,DAY) AS days_waiting_to_receive,
    DATE_DIFF(reviewed_at, received_at,DAY) AS days_waiting_to_review,
    NULL AS days_waiting_to_verify
  FROM
    ${ref("further_information_requests_latest_afqts")} AS further_information ),
  ps_days_in_status AS (
  SELECT
    assessment_id,
    "professional_standing" AS request_type,
    created_at,
    DATE_DIFF(requested_at, created_at,DAY) AS days_waiting_to_request,
    DATE_DIFF(received_at, requested_at,DAY) AS days_waiting_to_receive,
    DATE_DIFF(reviewed_at, received_at,DAY) AS days_waiting_to_review,
    DATE_DIFF(verified_at, reviewed_at,DAY) AS days_waiting_to_verify
  FROM
    ${ref("professional_standing_requests_latest_afqts")} AS professional_standing ),
  qr_days_in_status AS (
  SELECT
    assessment_id,
    "qualification_requests" AS request_type,
    created_at,
    DATE_DIFF(requested_at, created_at,DAY) AS days_waiting_to_request,
    DATE_DIFF(received_at, requested_at,DAY) AS days_waiting_to_receive,
    DATE_DIFF(reviewed_at, received_at,DAY) AS days_waiting_to_review,
    DATE_DIFF(verified_at, reviewed_at,DAY) AS days_waiting_to_verify
  FROM
    ${ref("qualification_requests_latest_afqts")} AS qualification_requests ),
  rr_days_in_status AS (
  SELECT
    assessment_id,
    "reference_requests" AS request_type,
    created_at,
    NULL AS days_waiting_to_request,
    DATE_DIFF(received_at, requested_at,DAY) AS days_waiting_to_receive,
    DATE_DIFF(reference_requests.reviewed_at, reference_requests.received_at,DAY) AS days_waiting_to_review,
    DATE_DIFF(reference_requests.verified_at, reference_requests.reviewed_at,DAY) AS days_waiting_to_verify
  FROM
    ${ref("reference_requests_latest_afqts")} AS reference_requests ),
  all_days_in_status AS (
  SELECT
    assessment_id,
    request_type,
    created_at,
    days_waiting_to_request,
    days_waiting_to_receive,
    days_waiting_to_review,
    days_waiting_to_verify
  FROM
    fi_days_in_status
  UNION ALL
  SELECT
    assessment_id,
    request_type,
    created_at,
    days_waiting_to_request,
    days_waiting_to_receive,
    days_waiting_to_review,
    days_waiting_to_verify
  FROM
    ps_days_in_status
  UNION ALL
  SELECT
    assessment_id,
    request_type,
    created_at,
    days_waiting_to_request,
    days_waiting_to_receive,
    days_waiting_to_review,
    days_waiting_to_verify
  FROM
    qr_days_in_status
  UNION ALL
  SELECT
    assessment_id,
    request_type,
    created_at,
    days_waiting_to_request,
    days_waiting_to_receive,
    days_waiting_to_review,
    days_waiting_to_verify
  FROM
    rr_days_in_status)
SELECT
  assessment_id,
  reference,
  DATE(submitted_at) AS submitted_at_date,
  CASE
    WHEN awarded_at IS NOT NULL THEN DATE(awarded_at)
    WHEN declined_at IS NOT NULL THEN DATE(declined_at)
  ELSE
  NULL
END
  AS assessed_date,
  DATE(started_at) AS started_at_date,
  DATE(recommended_at) AS recommended_at_date,
  country_name,
  teaching_authority_provides_written_statement,
  request_type,
  all_days_in_status.created_at,
  CAST(ROUND(days_waiting_to_request,0) AS INT64) AS days_waiting_to_request,
  CAST(ROUND(days_waiting_to_receive,0) AS INT64) AS days_waiting_to_receive,
  CAST(ROUND(days_waiting_to_review,0) AS INT64) AS days_waiting_to_review,
  CAST(ROUND(days_waiting_to_verify,0) AS INT64) AS days_waiting_to_verify
FROM
  all_days_in_status
LEFT JOIN
  ${ref("assessments_latest_afqts")} AS assessments
ON
  assessments.id = assessment_id
LEFT JOIN
  ${ref("application_forms_latest_afqts")} AS application_forms
ON
  assessments.application_form_id = application_forms.id
LEFT JOIN
  ${ref("joinGeoData")} AS joinGeoData
ON
  application_forms.region_id = joinGeoData.region_id
