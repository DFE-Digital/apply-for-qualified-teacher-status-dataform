config {
    type: "table",
    description: "QTS Application data with spokes unpivoted",
    columns: {
        id: "UID for wach application",
        created_at: "Date application was created",
        updated_at: "Date application was updated",
        reference: "QTS application reference",
        state: "Application state",
        teacher_id: "UID of teacher",
        region_id: "UID for region",
        submitted_at: "Date application was submitted",
        working_days_between_submitted_and_today: "Working days since application was created",
        spokes: "Application spoke",
        spoke_status: "Status of application spoke",
        country_eea:  "applicant qualified in EEA country - true / false",
        country_eu:  "applicant qualified in EU country - true / false"
    }
}

SELECT
  id,
  created_at,
  updated_at,
  reference,
  state,
  stage,
  statuses,
  teacher_id,
  application_forms.region_id AS application_region_id,
  submitted_at,
  working_days_between_submitted_and_today,
  spokes,
  spoke_status,
  country_id,
  name AS region_name,
  code,
  country_name,
  country_eea,
  country_eu
FROM
  ${ref("application_forms_latest_afqts")}
UNPIVOT
  ( spoke_status FOR spokes IN ( personal_information_status,
      identification_document_status,
      qualifications_status,
      age_range_status,
      subjects_status,
      work_history_status,
      registration_number_status,
      written_statement_status,
      english_language_status ) ) AS application_forms
LEFT JOIN
  ${ref("joinGeoData")} AS joinGeoData
ON
  application_forms.region_id = joinGeoData.region_id
